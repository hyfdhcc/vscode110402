ROUTINE ENVIRON [Type=INT]
ENVIRON ; Maintain global data for $ENVOPTION macro ; LFT477, 8/01/97
 /*
 +--------------------------------------------------------+
 | Copyright 1986-2015 by InterSystems Corporation,       |
 | Cambridge, Massachusetts, U.S.A.                       |
 | All rights reserved.                                   |
 |                                                        |
 | Confidential, unpublished property of InterSystems.    |
 |                                                        |
 | This media contains an authorized copy or copies       |
 | of material copyrighted by InterSystems and is the     |
 | confidential, unpublished property of InterSystems.    |
 | This copyright notice and any other copyright notices  |
 | included in machine readable copies must be reproduced |
 | on all authorized copies.                              |
 +--------------------------------------------------------+
 */
 /* Revision: $Id: //adhocs/2016.2.3.904.11/DHC_SOFTWARE_CO_001/databases/sys/inc/_syJrninc.inc#1 $ ($Change: 4521965 $) */
 ; Intention: return 1 if we're in an environment supporting
 ; big strings (i.e. has the global BDATA type 12 blocks and
 ; the 0x81 GBIGTYPE nodes).
 ; Implementation: ASSUME we have big string support if we can
 ; build a local string of length > 510.         
MAIN N ERROR S ERROR=0         
 W !,"Feature",?15,"Compiled for",?40,"Actual (running now)"
 D environ0("NOLONGSTRINGS",0,$$NOLONGSTRINGS)   
 D environ0("NOWILDCARDS",0,$$NOWILDCARDS)
 D environ0("NOLONGNAMES",0,$$NOLONGNAMES)   
 D environ0("NOUSENEWCOL",0,$$NOUSENEWCOL)
 D environ0("NOSETEXTRACT",0,$$NOSETEXTRACT)
 D environ0("NONEWLICENSE",0,$$NONEWLICENSE)
 D environ0("USECLUSTER",1,$$USECLUSTER)
 D environ0("NONETTRANSPROC",0,$$NONETTRANSPROC)
 D environ0("NOUSESLM",0,$$NOUSESLM)
 D environ0("NOUSEUSTART",0,$$NOUSEUSTART)
 W:ERROR !!,*7,"BAD CONFIGURATION" W:'ERROR !!,"Configuration OK"
 Q
environ0(name,compile,run) 
 s $zt="environe"
 w !,$$basename(name),?15,$$no(name,compile),?40,$$no(name,run)
 s:compile'=run ERROR=1
 q
environe s $zt=""
 w !,$$basename(name),?15,$p($ze,">")_"> -- unknown status"
 q         
basename(name) n p s p=$p(name,"NO",2) q:p'="" p q name
no(name,v) 
 n no s no=$p(name,"NO",2)'="" s:v=0 no='no q:no "NO" q "YES" 
FEATURES() Q "NOLONGSTRINGS NOWILDCARDS NOLONGNAMES NOUSENEWCOL NOSETEXTRACT NONEWLICENSE USECLUSTER NONETTRANSPROC NOUSESLM NOUSEUSTART"
 ; Report compile-time settings
SHOW W !,$name(^mutil("ENVIRON"))," is set as follows:"
 D SHOW0($name(^mutil("ENVIRON")))
 Q
SHOW0(NODE) 
 N I,F,T,V
 W !!,"Status",?8,"Feature",?25,$name(^mutil("ENVIRON"))," setting"
 F I=1:1:$L($$FEATURES," ") S F=$P($$FEATURES," ",I) D
 . S V=$D(@NODE@(F))'=0
 . w !,$$no(F,V),?8,$$basename(F)
 . W ?25,$name(@NODE@(F))," ",$S(V:"defined",1:"undefined (default)")
 Q
SET ; Set compile-time settings
 N I,F,NODE,TEMP,V
 D SHOW S NODE=$name(^mutil("ENVIRON"))
 W !!,"New settings:",!
 F I=1:1:$L($$FEATURES," ") S F=$P($$FEATURES," ",I) D
 . I $$YN(F,"N")="Y" S TEMP(F)="" 
 D SHOW0("TEMP")
 I $$YN("Make the change")="Y" K @NODE M:$D(TEMP) @NODE=TEMP 
 Q
NOLONGSTRINGS() Q '$$LEGAL("$J(1,500)_$J(2,500)")
 ; $ZU(78,25,...) is a new function for NETTRANSPROC. Since we
 ; haven't opened a journal file we'll probably get a <NOTOPEN> error.
 ; If this isn't supported, we'll get a <FUNCTION> error
NONETTRANSPROC() ;
 s $ZT="NETTPERR"
 i $ZU(78,25,0) ; we expect <function> if not supported
 q 0  ; it is supported, return failure
NETTPERR ;
 s $ZT=""
 i $ZE["<NOTOPEN" q 0  ; it is supported, return failure
 q 1  ; most likely function error, return success, it isn't supported
 ; Returns maximum string length
MAXLEN() N N,STEP
 S N=512 F  Q:'$$legallen(2*N)  S N=2*N
 F  Q:$$legallen(N)  S N=N\2 ;In case N=256 was too big 
 S STEP=N 
 F  S STEP=STEP\2 S:$$legallen(N+STEP) N=N+STEP Q:STEP=1
 Q N
 ; Would just be Q $$LEGAL("$J($C(32),N)") if not for bug
legallen(N) Q:'$$LEGAL("$J($C(32),N)") 0
    N TEST S TEST=$J($C(32),N) Q $L(TEST)=N   ;Needed due to bug
NOWILDCARDS() Q '$$LEGAL("$ZU(90,0)")
NOLONGNAMES() N ABCDEFGH,ABCDEFGHI
 S ABCDEFGH=0,ABCDEFGHI=1
 Q ABCDEFGH=ABCDEFGHI
NOUSENEWCOL() Q '$$LEGAL("$ZU(23,0)")
USECLUSTER() Q $$LEGAL("$ZU(59)")
NOUSESLM() Q '$$LEGAL("$ZU(90,11,1,""ABC"")")
 ; $$LEGAL(EXPRESS) returns 1 if evaluating EXPRESS causes no error.
 ; E.g. $$LEGAL("$ZU(90,0)")
LEGAL(EXP) 
 N V,XEQEXP S XEQEXP="V="_EXP,$ZT="legal0",@XEQEXP Q 1
legal0 Q 0 
NOSETEXTRACT() N % S $ZT="NOSETERR" S $E(%)="" Q 0
NOSETERR Q 1
NONEWLICENSE() N % S $ZT="NOLICERR" S %=$ZU(200,15) Q 0
NOLICERR Q 1
NOUSEUSTART()
 s $ZT="USTARTERR"
 i $ZU(40,2,115) q 0  ; it is supported, return failure
USTARTERR
 q 1  
YN(P,D,t) N R,X S P=$G(P),D=$E($G(D)_"?"),t=$G(t) ;ask Yes/No w/ Prompt,Default
 S D=$S("Yy"[D:"Yes => ","Nn"[D:"No => ",1:"") ;Yes, No or no default
 F  W !,P_"? "_D Read:t R:t Read:'t R DO  I "^YN"[R Q  ;timed or non-timed read
 . S X=R,R=$TR($E(R_D_"?"),"yn","YN") I "^YN"'[R W "  enter Yes or No, please"
 S POP=(R="^") W $E($S(R="N":"No",R="Y":"Yes",1:""),$L(X)+1,3) Q R