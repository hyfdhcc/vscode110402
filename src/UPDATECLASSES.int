ROUTINE UPDATECLASSES [Type=INT]
UPDATECLASSES ; Update all classes in all namespaces ; STC745 1/10/05
 /*
 +--------------------------------------------------------+
 | Copyright 1986-2015 by InterSystems Corporation,       |
 | Cambridge, Massachusetts, U.S.A.                       |
 | All rights reserved.                                   |
 |                                                        |
 | Confidential, unpublished property of InterSystems.    |
 |                                                        |
 | This media contains an authorized copy or copies       |
 | of material copyrighted by InterSystems and is the     |
 | confidential, unpublished property of InterSystems.    |
 | This copyright notice and any other copyright notices  |
 | included in machine readable copies must be reproduced |
 | on all authorized copies.                              |
 +--------------------------------------------------------+
 */
 	n
	w !,"This routine will update and compile classes in"
	w !,"all namespaces on your system if necessary.",!
 	d List^%SYS.NAMESPACE(.nslist)
	s ns="" f  s ns=$o(nslist(ns)) q:ns=""  d
	. i "DOCBOOK,SAMPLES"[ns k nslist(ns) q
	. d CHECKUPDATE
	i '$d(nslist) w !,"Classes in all namespaces are up to date" q
	i $d(^%SYS("RECOMPILECLASSES")) d  i $$YN("Do you want to delete previous update information","N")="N" q
	. w !,"A previous class update was run which failed, or there is"
	. w !,"a class update currently running."
	. w !!,"The following namespaces are queued for updating:"
	. s ns="" f  s ns=$o(^%SYS("RECOMPILECLASSES","QUEUE",ns)) q:ns=""  w !,ns
	. w !!,"The following namespaces are being updated:"
	. s ns="" f  s ns=$o(^%SYS("RECOMPILECLASSES","RUN",ns),1,Value) q:ns=""  w !,ns,"   "_Value
	. w !!,"The following namespaces are updated:"
	. s ns="" f  s ns=$o(nscvt(ns)) q:ns=""  w !,ns
	k ^%SYS("RECOMPILECLASSES")
	w !!,"The following namespaces require that the classes be recompiled:",!
	s ns="" f  s ns=$o(nslist(ns)) q:ns=""  w !,ns
	s back=0
	w !
	i $$YN("Compile the classes in the background","Y")="Y" s back=1 
	i $$YN("Confirm recompiling classes","N")="N" q
	s ns="",Count=0 f  s ns=$o(nslist(ns)) q:ns=""  d
	. s ^%SYS("RECOMPILECLASSES","QUEUE",ns)=""
	. i Count<10 s Count=Count+1
	i back=1 d  q
	. w !,"Starting background processes"
	. f i=1:1:Count j UPGRADEOBJECTS(back)::5 i $t w "."
	. w !,"Examine the CCONSOLE.LOG file for errors"
	d UPGRADEOBJECTS(back)
	q
CHECKUPDATE s $zt="CHECKUPDATEE"
	s oldns=$zu(5)
	d $zu(5,ns)
	Do:($g(^oddDEF)'=25) SetCompatibleClassDefVersion^%occVersion() s ismatched=$g(^oddDEF,25)=25
	d $zu(5,oldns)
	i ismatched k nslist(ns) s nscvt(ns)=""
	q
CHECKUPDATEE s $zt=""
	i $d(oldns) d $zu(5,oldns)
	k nslist(ns)
	q
UPGRADEOBJECTS(back)
UPGRADEOBJECTS1	s $zt="UPGRADEOBJECTSE"
 	l +^%SYS("RECOMPILECLASSES")
	s ns=$o(^%SYS("RECOMPILECLASSES","QUEUE",""))
	i ns'="" s ^%SYS("RECOMPILECLASSES","RUN",ns)="" k ^%SYS("RECOMPILECLASSES","QUEUE",ns)
 	l -^%SYS("RECOMPILECLASSES")
	q:ns=""
	s oldns=$zu(5)
	d $ZU(5,ns)
	i back s file="UPDATECLASSES_"_$J_".LOG" o file:"WNS" u file
	w !,"Updating classes in namespace "_ns,!
	d $SYSTEM.OBJ.Upgrade()
	d $SYSTEM.SQL.Purge(0)
	d $SYSTEM.OBJ.CompileAll()
	s app="" f  s app=$o(^%SYS("CSP","Applications",app),1,value) q:app=""  d
	. q:$zcvt($li(value,9),"U")'=$zcvt(ns,"U")
	. i $li(value,4)=0 w !,"Auto Compile not set for application "_app q
	. d $SYSTEM.CSP.LoadPageDir(app)
	w !,"Classes updated in namespace "_ns
	i back c file d UPDATECONSOLELOG(file)
	d $zu(5,oldns)
	k ^%SYS("RECOMPILECLASSES","RUN",ns)
	g UPGRADEOBJECTS1
	q
UPGRADEOBJECTSE s $zt=""
 	l -^%SYS("RECOMPILECLASSES")
	w !,"ERROR: "_$ze
	s ^%SYS("RECOMPILECLASSES","RUN",ns)=$ze
	i back c file d UPDATECONSOLELOG(file)
	d $zu(5,oldns)
	q	
UPDATECONSOLELOG(file)
	s $zt="UPDATECONSOLELOGE"
	l +^%CCONSOLE
	o file:"R"
	s file1=$zu(12)_"cconsole.log"
 	o file1:"WAS":2 q:'$t
	f  u file r x g:$zeof UPDATECONSOLELOGE u file1 w !,x
UPDATECONSOLELOGE s $zt=""
	l -^%CCONSOLE
	c file
	c file1
	i $zu(140,5,file)
	q
YN(P,D,t) N R,X S P=$G(P),D=$E($G(D)_"?"),t=$G(t) ;ask Yes/No w/ Prompt,Default
 S D=$S("Yy"[D:"Yes => ","Nn"[D:"No => ",1:"") ;Yes, No or no default
 F  W !,P_"? "_D Read:t R:t Read:'t R DO  I "^YN"[R Q  ;timed or non-timed read
 . S X=R,R=$TR($E(R_D_"?"),"yn","YN") I "^YN"'[R W "  enter Yes or No, please"
 S POP=(R="^") W $E($S(R="N":"No",R="Y":"Yes",1:""),$L(X)+1,3) Q R