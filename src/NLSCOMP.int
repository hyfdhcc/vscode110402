ROUTINE NLSCOMP [Type=INT]
NLSCOMP   ; FULLNLS compiler ; JLC1602 05/03/12
 /*
 +--------------------------------------------------------+
 | Copyright 1986-2015 by InterSystems Corporation,       |
 | Cambridge, Massachusetts, U.S.A.                       |
 | All rights reserved.                                   |
 |                                                        |
 | Confidential, unpublished property of InterSystems.    |
 |                                                        |
 | This media contains an authorized copy or copies       |
 | of material copyrighted by InterSystems and is the     |
 | confidential, unpublished property of InterSystems.    |
 | This copyright notice and any other copyright notices  |
 | included in machine readable copies must be reproduced |
 | on all authorized copies.                              |
 +--------------------------------------------------------+
 */
 ;%ShmObj.INC ; JLC1084 11/07/07
 Quit
Table(ttyp,fromenc,name,sref) New oid,ref,t1 Set $zt="%CompERROR" Set t1=$zh
 Set:'$d(sref) sref="^%nls"
 Kill ^CacheTempJ($j) Set ref=$name(@sref@("Src",ttyp,fromenc,name))
 If $e($g(@ref),1,8)="Built-in" Write:$d(^%nlsmisc("DEBUG")) "Built-in   ",ref,! Quit -1
 If $d(@ref)<10 Write:$d(^%nlsmisc("DEBUG")) "Not loaded ",ref,! Quit -2
 Do %XLT:ttyp="XLT"!(ttyp["Case"),%Ident:ttyp="Ident",%Pattern:ttyp="Pattern",%XY:ttyp="XY",%COL:ttyp="COL"
 If '$g(oid) Write:$d(^%nlsmisc("DEBUG")) !,"Unable to create "_$s($g(ttyp)="XLT":"Translation",$g(ttyp)="XY":"$X/$Y Behaviour",$g(ttyp)="Ident":"Identifier",$g(ttyp)="Pattern":"Pattern Match",$g(ttyp)="UpperCase":"Upper case",$g(ttyp)="LowerCase":"Lower case",$g(ttyp)="TitleCase":"Title case",1:"Collation")_" table "_$g(fromenc)_" to "_$g(name)_"." Quit 0
 If oid=-2 Write:$d(^%nlsmisc("DEBUG")) "Not loaded ",ref,! Quit -2 ; JLC078
 Set ^SYS("shm","Src",0,oid,"%nls",ttyp,fromenc,name)=""
 Set ^SYS("shm","Src",1,"%nls",ttyp,fromenc,name,oid)=""
 New x
 For  Set x=$O(^SYS("shm","Src",1,"%nls",ttyp,fromenc,name,oid)) Quit:x=""  Kill ^(x),^SYS("shm","Src",0,x,"%nls",ttyp,fromenc,name)
 Write:$d(^%nlsmisc("DEBUG")) "Compiled   ",ref," in ",$ZH-t1," seconds",!
 Quit oid
Locale(loc,sref) New nam,num,typ,rtyp,from,to,ref,oid,charset
 Set:'$d(sref) sref="^%nls"
 Set charset=$g(@sref@("Loc",loc,0))
 If charset="" Quit 0
 Set num=0,ref=$name(@sref@("Loc",loc,1)),typ=""
 For  Set typ=$o(@ref@(typ)) Quit:typ=""  Set nam="" Do
 . For  Set nam=$o(@ref@(typ,nam)) Quit:nam=""  Do
 . . If typ="Ident"!(typ="XY")!(typ="Pattern")!(typ["Case") Do
 . . . Set oid=$$Table(typ,charset,nam,sref) Set:oid num=num+1
 . . Else  If typ="XLT"!(typ="COL") Do
 . . . If $d(@ref@(typ,nam))<10 Set from=charset,to=nam Do
 . . . . Set oid=$$Table(typ,from,to,sref) Set:oid num=num+1
 . . . . Set oid=$$Table(typ,to,from,sref) Set:oid num=num+1
 . . . Else  Do
 . . . . Set from=$g(@ref@(typ,nam,0,0)),to=$g(@ref@(typ,nam,0,1))
 . . . . If from'="",to'="" Set oid=$$Table(typ,from,to,sref) Set:oid num=num+1
 . . . . Set from=$g(@ref@(typ,nam,1,0)),to=$g(@ref@(typ,nam,1,1))
 . . . . If from'="",to'="" Set oid=$$Table(typ,from,to,sref) Set:oid num=num+1
 Quit num
All(sref) New ttyp,fromenc,name,oid Set fromenc="",name=""
 Set:'$D(sref) sref="^%nls"
 For ttyp="Ident","XY","Pattern","XLT","COL","LowerCase","UpperCase","TitleCase" Do
 . For  Set fromenc=$o(@sref@("Src",ttyp,fromenc)) Quit:fromenc=""  Do
 . . For  Set name=$o(@sref@("Src",ttyp,fromenc,name)) Quit:name=""  Do
 . . . Set oid=$$Table(ttyp,fromenc,name,sref)
 Quit
%Ident Set oid=$$%CompBITS($name(@ref@(0))) Quit
%Pattern If '$d(@ref@(1)) Set oid=-2 Quit
 New from,rng,i,lo,patcode,pattbl,val,pref
 Set from="",patcode="",pattbl=@ref@(0),$ZT="%XLTERROR"
 Set pref=$Name(@sref@("Src","Patcode",pattbl))
 If '$d(@pref@(0)) Set oid=-2 Quit
 For {
   Set patcode=$o(@ref@(1,patcode)) Quit:patcode=""
   Set val=+$g(@pref@(0,patcode))
   For {
	 Set from=$o(@ref@(1,patcode,from)) Quit:from=""  Set rng=^(from)
	 If from<65536 {
       For i=from:1:from+rng-1 Set lo=i#256 Set ^(lo)=$zboolean(+val,+$g(^CacheTempJ($j,"bmp",i\256,lo)),7)
	 } Else {
       For i=from-65536:1:from-65536+rng-1 Set lo=i#1024 Set ^(lo)=$zboolean(+val,+$g(^CacheTempJ($j,"sur",i\1024,lo)),7)
	 }
   }
 }
 Set oid=$$%CompVAL($name(^CacheTempJ($j)),1) Quit
%XY New from,rng,i,dxy Set dxy="",from="",$ZT="%XLTERROR"
 For  Set dxy=$o(@ref@(0,dxy)) Quit:dxy=""  Do
 . For  Set from=$o(@ref@(0,dxy,from)) Quit:from=""  Set rng=^(from) Do
 . . For i=from:1:from+rng-1 Set ^CacheTempJ($j,"bmp",i\256,i#256)=dxy
 Set oid=$$%CompVAL($name(^CacheTempJ($j)),1) Quit
%XLT New obj,typ,act,com,val,err,tmp,byt,top,i,pag,sur
 Set err=0,$ZT="%XLTERROR",typ=@ref@(0)
 Goto %XLTNormal:typ=0,%XLTToModal:typ=1,%XLTFromModal:typ=2
%XLTERROR SET $ZT="" Do ErrMsg^NLSLOAD("Error while trying to create "_$s($g(ttyp)="XLT":"Translation",$g(ttyp)="XY":"$X/$Y Behaviour",$g(ttyp)="Ident":"Identifier",$g(ttyp)="Pattern":"Pattern Match",$g(ttyp)="UpperCase":"Upper case",$g(ttyp)="LowerCase":"Lower case",$g(ttyp)="TitleCase":"Title case",1:"Collation")_" table "_$g(fromenc)_" to "_$g(name)_".")
 Quit
%XLTNormal Set sur=0,oid=$$%CompSUB(@ref@(4),.byt,.top,.pag,.sur) Quit:oid<1
 If sur Set sur=$$%CompSUB(@ref@(4),.byt,.top,.pag,.sur) Quit:sur<1
 Set typ=+$g(^SYS("shm","Obj",oid)),obj(3)=oid,oid=0 Goto:$d(byt) %XLTPack
 Set act=+$g(@ref@(2))
 Set obj=9,com=($d(pag)=0)*2+(ttyp["Case")+1
 If act=2 Set obj(2)=+$g(@ref@(3)),val=3
 Else  Set val(0)=$$%CompStr($g(@ref@(3)),.val),obj(2)=$$Save^ShmObj(.val) Quit:'obj(2)  Set val=^SYS("shm","Obj",obj(2))
 Set obj(0)=$C(1,val,typ)
 If fromenc="UnicodeLit" Set obj(1)=$C(5,act)
 Else  If fromenc="UnicodeBig" Set obj(1)=$C(7,act)
 Else  If name="UnicodeLit" Set obj(1)=$C(4,act)
 Else  If name="UnicodeBig" Set obj(1)=$C(6,act)
 Else  If name="GB18030" Set obj(1)=$C(26,act)
 Else  If fromenc="Latin1",name="UTF8" Set obj(1)=$C(2,act)
 Else  If fromenc="UTF8",name="Latin1" Set obj(1)=$C(3,act)
 Else  Set obj(1)=$E($C(13,11,12,10),com)_$C(act)
 If sur Set obj(4)=sur,obj(0)=obj(0)_$C(^SYS("shm","Obj",obj(4)))
 Set oid=$$Save^ShmObj(.obj)
 Quit
%XLTPack Quit:'$d(top)  Quit:'top  Goto %XLTGB18030:fromenc="GB18030"
 Set obj(5)=top
 Set obj(1)=$C(14,+$g(@ref@(2)))
 Set val(0)=$$%CompStr($g(@ref@(3)),.val)
 Set obj(2)=$$Save^ShmObj(.val) Quit:'obj(2)
 Set byt(0)=byt,byt=10,obj(4)=$$Save^ShmObj(.byt) Quit:'obj(4)
 Set obj(0)=$C(1,^SYS("shm","Obj",obj(2)),typ,^SYS("shm","Obj",obj(4)),^SYS("shm","Obj",top))
 Set obj=9 Set oid=$$Save^ShmObj(.obj)
 Quit
%XLTGB18030	Set obj(4)=top
 Set obj(1)=$C(27,+$g(@ref@(2)))
 Set val(0)=$$%CompStr($g(@ref@(3)),.val)
 Set obj(2)=$$Save^ShmObj(.val) Quit:'obj(2)
 Set obj(0)=$C(1,^SYS("shm","Obj",obj(2)),typ,^SYS("shm","Obj",top))
 Set obj=9 Set oid=$$Save^ShmObj(.obj)
 Quit
%XLTToModal Set sur=0,oid=$$%CompSUB(@ref@(4),.byt,.top,.pag,.sur) Quit:oid<1
 If sur Set sur=$$%CompSUB(@ref@(4),.byt,.top,.pag,.sur) Quit:sur<1
 Set typ=+$g(^SYS("shm","Obj",oid)),act=+$g(@ref@(2))
 Set obj=9,obj(3)=oid,oid=0
 Set val(0)=$$%CompStr($g(@ref@(3)),.val)
 Set obj(2)=$$Save^ShmObj(.val) Quit:'obj(2)
 Set obj(1)=$C($S($d(pag)=0:16,1:17),act)
 Set act=$name(@sref@("Src","Mode",@ref@(5)))
 Set val(0)="",typ(0)=""
 For com=0:1:$o(@act@(1,""),-1) Do  Quit:err
 . Set val(0)=val(0)_$c(@act@(1,com))
 . Set tmp(0)=$$%CompStr(@act@(2,com),.tmp),typ(0)=typ(0)_$c(tmp)
 . Set typ(com+1)=$$Save^ShmObj(.tmp) If 'typ(com+1) Set err=1 Quit
 Quit:err
 Set val=5,obj(4)=$$Save^ShmObj(.val) Quit:'obj(4)
 Set typ=9,obj(5)=$$Save^ShmObj(.typ) Quit:'obj(5)
 Set obj(0)=$C(1,^SYS("shm","Obj",obj(2)),typ,^SYS("shm","Obj",obj(4)),^SYS("shm","Obj",obj(5)))
 If sur Set obj(6)=sur,obj(0)=obj(0)_$C(^SYS("shm","Obj",obj(6)))
 Set oid=$$Save^ShmObj(.obj)
 Quit
%XLTFromModal Set obj=9
 If '$d(@sref@("Src","Mode",@ref@(5),1)) Set oid=-2 Quit  ; JLC078
 Set val(0)=$$%CompStr($g(@ref@(3)),.val)
 Set obj(2)=$$Save^ShmObj(.val) Quit:'obj(2)
 Set obj(0)=$C(1,^SYS("shm","Obj",obj(2)),9,5,9)
 Set obj(1)=$C(15,+$g(@ref@(2)))
 Set act=$name(@sref@("Src","Mode",@ref@(5)))
 Set val(0)="",typ(0)="",com(0)=""
 For com=0:1:$o(@act@(1,""),-1) Do  Quit:err
 . Set val=+@act@(1,com),val(0)=val(0)_$c(val)
 . Set tmp(0)=$$%CompStr(@act@(2,com),.tmp),typ(0)=typ(0)_$c(tmp)
 . Set typ(com+1)=$$Save^ShmObj(.tmp) If 'typ(com+1) Set err=1 Quit
 . Kill byt,top Set tmp=$$%CompSUB(@act@(0,com),.byt,.top)
 . If 'tmp Set err=2 Quit
 . Set com(0)=com(0)_$c(^SYS("shm","Obj",tmp)),com(com+1)=tmp
 . If $d(byt) For i=1:1:256 If $A(byt,i),$A(byt,i)'=val Set err=3 Quit
 . If val=1,'$d(top),$d(byt) Set err=4 Quit
 . If val'=1,$d(top) Set err=5 Quit
 Quit:err
 Set com=9,obj(3)=$$Save^ShmObj(.com) Quit:'obj(3)
 Set val=5,obj(4)=$$Save^ShmObj(.val) Quit:'obj(4)
 Set typ=9,obj(5)=$$Save^ShmObj(.typ) Quit:'obj(5)
 Set oid=$$Save^ShmObj(.obj)
 Quit
%CompERROR Set $ZT="" Do ErrMsg^NLSLOAD("Error while trying to create "_$s($g(ttyp)="XLT":"Translation",$g(ttyp)="XY":"$X/$Y Behaviour",$g(ttyp)="Ident":"Identifier",$g(ttyp)="Pattern":"Pattern Match",$g(ttyp)="UpperCase":"Upper case",$g(ttyp)="LowerCase":"Lower case",$g(ttyp)="TitleCase":"Title case",1:"Collation")_" table "_$g(fromenc)_" to "_$g(name)_".")
 Quit 0
%CompSUB(name,byt,top,pag,sur) New (name,byt,top,pag,sur,sref)
 Set typ=@sref@("Src","SUB",name,0),sur=+$G(sur)
 If sur { Set bas=65536,siz=1024,ini=65535,end=$ZH("10FFFF") }
 Else { Set bas=0,siz=256,ini="",end=65535 }
 If '$d(@sref@("Src","SUB",name,1)) Quit -2         ; JLC078
 Goto %SUBSingle:typ=0,%SUBToCol:typ=5,%SUBFromCol:typ=6,%SUBMulti:typ=3
 Goto:$zboolean(+typ,+2,1) %SUBFromMulti
 Goto:$zboolean(+typ,+1,1) %SUBToMulti
 Write:$d(^%nlsmisc("DEBUG")) !,"Error - Table type: ",$P("Single,ToMulti,FromMulti,Multi",",",$zboolean(+typ,+3,1)+1)," not yet supported"
 Quit 0
%SUBSingle Set src=$name(@sref@("Src","SUB",name,1)),$ZT="%CompERROR"
 If 'sur {
	 Set i=$o(@src@(""),-1)
	 If i<256 Set pag=1 Quit $$%CompPAG(src)
	 If i>65535 Set sur=1
 }
 Set i=ini,dst=$name(^CacheTempJ($j,"SUB")) Kill @dst
 For  Set i=$o(@src@(i)) Quit:i=""||(i>end)  Set j=i-bas,@dst@(j\siz,j#siz)=@src@(i)
 Set obj=$S(sur:29,1:19),$p(obj(0),$c(0),siz+1)="",i=""
 For  Set i=$o(@dst@(i)) Quit:i=""  Set oid=$$%CompPAG($name(@dst@(i)),siz) Do:oid
 . Set obj(i+1)=oid,$e(obj(0),i+1)=$c(^SYS("shm","Obj",oid))
 If $o(obj(1))="" Set pag=1 Quit +$g(obj(1))
 Quit $$Save^ShmObj(.obj)
%SUBToMulti Set src=$name(@sref@("Src","SUB",name,1)),$ZT="%CompERROR"
 If 'sur {
	 Set i=$o(@src@(""),-1)
	 If i<256 Set pag=1 Quit $$%CompPAGMulti(src,1)
	 If i>65535 Set sur=1
 }
 Set i=ini,dst=$name(^CacheTempJ($j,"SUB")) Kill @dst
 For  Set i=$o(@src@(i)) Quit:i=""||(i>end)  Set j=i-bas,@dst@(j\siz,j#siz)=@src@(i)
 Set obj=$S(sur:29,1:19),$p(obj(0),$c(0),siz+1)="",i=""
 For  Set i=$o(@dst@(i)) Quit:i=""  Set oid=$$%CompPAGMulti($name(@dst@(i)),'i,siz) Do:oid
 . Set obj(i+1)=oid,$e(obj(0),i+1)=$c(^SYS("shm","Obj",oid))
 If $o(obj(1))="" Set pag=1 Quit +$g(obj(1))
 Quit $$Save^ShmObj(.obj)
%SUBFromMulti Set $ZT="%CompERROR"
 Goto %SUBFromGB18030:name="XLT-GB18030-Unicode"
 Set src=$name(@sref@("Src","SUB",name,1)),lst=$o(@src@(""),-1) zt:lst>255 "COMP"
 Set ns=$ql(sref)
 Set $p(byt,$c(0),257)="",nod=src
 For  Set nod=$q(@nod) Quit:$qs(nod,ns+2)'="SUB"  Quit:$qs(nod,ns+3)'=name  Quit:$qs(nod,ns+4)>lst  Do
 . Set i=+$qs(nod,ns+5),len=$ql(nod)-ns-4
 . If '$a(byt,i+1) Set $e(byt,i+1)=$c(len)
 . Else  zt:$a(byt,i+1)'=len "COMP"
 For i=0:1:255 Set:$a(byt,i+1)=1 myval(i)=@src@(i)
 If $d(myval) Set top=$$%CompPAGMulti("myval")
 Set $p(obj(0),$c(0),257)=""
 For i=1:1:256 Set:$a(byt,i)>1 obj(i)=$$%CompLEV($a(byt,i),$name(@src@(i-1)))
 For i=1:1:256 Set:$d(obj(i)) $e(obj(0),i)=$c(^SYS("shm","Obj",obj(i)))
 Set obj=19 Quit $$Save^ShmObj(.obj)
%SUBFromGB18030	Set ns=$ql(sref),byt=1
 Set src=$name(@sref@("Src","SUB",name,1)),lst=$o(@src@(""),-1) zt:lst>255 "COMP"
 Set nod=src,$p(obj(0),$c(0),257)=""
 For  Set nod=$q(@nod) Quit:$qs(nod,ns+2)'="SUB"  Quit:$qs(nod,ns+3)'=name  Quit:$qs(nod,ns+5)>lst  Do
 . If $ql(nod)=6 Set gb($qs(nod,5),$qs(nod,6))=@nod
 For i=1:1:256 Set:$d(gb(i-1)) obj(i)=$$%CompLEV(2,$name(gb(i-1)))
 For i=1:1:256 Set:$d(obj(i)) $e(obj(0),i)=$c(^SYS("shm","Obj",obj(i)))
 Set obj=19,oid=$$Save^ShmObj(.obj)
 Kill obj,gb
 Set nod=src,$p(obj(0),$c(0),257)=""
 For  Set nod=$q(@nod) Quit:$qs(nod,ns+2)'="SUB"  Quit:$qs(nod,ns+3)'=name  Quit:$qs(nod,ns+5)>lst  Do
 . If $ql(nod)=8 Set gb($qs(nod,5),$qs(nod,6),$qs(nod,7),$qs(nod,8))=@nod
 For i=1:1:256 Set:$d(gb(i-1)) obj(i)=$$%CompLEV(4,$name(gb(i-1)))
 For i=1:1:256 Set:$d(obj(i)) $e(obj(0),i)=$c(^SYS("shm","Obj",obj(i)))
 Set obj=19,top=$$Save^ShmObj(.obj)
 Quit oid
%CompLEV(lev,ref) Quit:lev=2 $$%CompPAK(ref) New obj,i
 For i=0:1:255 Set:$d(@ref@(i)) obj(i+1)=$$%CompLEV(lev-1,$name(@ref@(i)))
 Set $p(obj(0),$c(0),257)=""
 For i=1:1:256 Set:$d(obj(i)) $e(obj(0),i)=$c(^SYS("shm","Obj",obj(i)))
 Set obj=19 Quit $$Save^ShmObj(.obj)
%COL New obj,typ,byt,top,pag
 Set $ZT="%CompERROR",oid=$$%CompSUB(@ref@(4),.byt,.top,.pag) Quit:oid<1
 Set obj(1)=$s($d(pag):$C(13),1:$C(12))
 Set typ=+$g(^SYS("shm","Obj",oid)),obj(3)=oid Goto:$d(byt) %XLTPack
 Set obj=9
 Set obj(0)=$C(1,3,typ)
 Set obj(1)=obj(1)_$C(+$g(@ref@(2)))
 Set obj(2)=$C(0)
 Set oid=$$Save^ShmObj(.obj)
 Quit
%SUBToCol Set sbt=0 Goto %SUBCol
%SUBFromCol Set sbt=1 Goto %SUBCol
%SUBMulti Set sbt=2 Goto %SUBCol
%SUBCol New obj,oid,i,dst,out
 Set src=$name(@sref@("Src","SUB",name,1)),$ZT="%CompERROR"
 Set i="",dst=$name(^CacheTempJ($j,"SUB")) Kill @dst
 For  Set i=$o(@src@(i)) Quit:i=""  Merge @dst@(i\256,i#256)=@src@(i)
 Set obj=19,$p(obj(0),$c(0),257)=""
 For  Set i=$o(@dst@(i)) Quit:i=""  Set oid=$$%CompPAGCol($name(@dst@(i)),sbt) Do:oid
 . Set obj(i+1)=oid,$e(obj(0),i+1)=$c(^SYS("shm","Obj",oid))
 If $o(obj(1))=""  Set pag=1 Quit +$g(obj(1)) 
 Quit $$Save^ShmObj(.obj)
%CompPAGCol(ref,sbt) New hi,obj,oid Set hi="",obj=19,$ZT="%CompERROR"
 Set $p(obj(0),$c(0),257)=""
 For  Set hi=$o(@ref@(hi)) Quit:hi=""  Set oid=$$%CompMultiStruct($name(@ref@(hi)),sbt) Do:oid
 . Set obj(hi+1)=oid,$e(obj(0),hi+1)=$c(^SYS("shm","Obj",oid))
 Quit $$Save^ShmObj(.obj)
%CompMultiStruct(ref,sbt) New obj,nod,bas,from,to,val,i,ns,p,yes,no,opt Set $ZT="%CompERROR"
 Set ns=$ql(ref),bas=$qs(ref,ns),obj(0)=$c(2),i=2
 Set nod=ref,yes=0,no=0,opt=1 Set:$d(@nod)#10=0 nod=$q(@nod)
 For  Quit:nod=""  Quit:$qs(nod,ns)'=bas  Set to=@nod Do  Set nod=$q(@nod)
 . If to[";" Set yes=yes+1
 . Else  Set no=no+1
 . If sbt=1,ns+1<$ql(nod) Set opt=0
 . Set dis=+$p(to,";",2),to=" "_$p(to,";",1)
 . If dis ZT:+$g(out(to),1)=0 "COMP"
 . Else  ZT:$d(out(to))&(sbt'=2) "COMP"
 . Set out(to)=dis
 If 'yes,'no ZT "COMP"
 If sbt=2,yes ZT "COMP"
 If sbt=0 Set:'yes sbt=2
 If sbt=1 Do
 . If no,'yes Set sbt=2
 . If 'no,yes Set:opt sbt=3
 If sbt'=3 Do
 . Set nod=ref Set:$d(@nod)#10=0 nod=$q(@nod)
 . For  Quit:nod=""  Quit:$qs(nod,ns)'=bas  Do  Set nod=$q(@nod)
 . . Set from=$e(nod,1,$l(nod)-1),from=$p(from,",",ns+1,999)
 . . If sbt=1,@nod'[";",from[",",$p(from,",",$l(from,","))'=0 Set from=from_",0"
 . . Set val(0)=$$%CompStr(from,.val),obj(i)=$$Save^ShmObj(.val)
 . . Set obj(0)=obj(0)_$c(^SYS("shm","Obj",obj(i))),i=i+1
 Set nod=ref Set:$d(@nod)#10=0 nod=$q(@nod)
 For  Quit:nod=""  Quit:$qs(nod,ns)'=bas  Do  Set nod=$q(@nod)
 . Set to=@nod,p=$f(to,";")
 . If p Set:sbt=0 $e(to,p-1)="," Set:sbt=1!(sbt=3) to=$p(to,";",1)
 . Else  If sbt=0,to[",",$p(to,",",$l(to,","))'=0 Set to=to_",0"
 . Set val(0)=$$%CompStr(to,.val),obj(i)=$$Save^ShmObj(.val)
 . Set obj(0)=obj(0)_$c(^SYS("shm","Obj",obj(i))),i=i+1
 Set obj=9,obj(1)=$C(sbt)
 Quit $$Save^ShmObj(.obj)         
%CompPAK(ref) New myval,i Set $ZT="%CompERROR"
 For i=0:1:255 Set:$d(@ref@(i))#2 myval(i)=@ref@(i)
 Quit $$%CompPAGMulti("myval")
%CompBITS(ref) New max,from,to,bits,obj,i,val Set $ZT="%CompERROR"
 Set max=-1,from=$o(@ref@(""))
 For  Quit:from=""  Set to=^(from)+from-1 zt:to<from "COMP" Set:to>max max=to Set from=$o(^(from))
 zt:(max<0)!(max>65535) "COMP" Goto:max<256 %CBYT2BIT
 Set max=(max+2047)\2048,bits=$ZBITSTR(max*2048),from=""
 For  Set from=$o(^(from)) Quit:from=""  Set bits=$ZBITSET(bits,from+1,1,^(from))
 Set $p(max,$c(0),257)=""
 Set val=10 For i=0:1:max-1 Set val(0)=$e(bits,i*256+2,i*256+257) Do
 . Set:val(0)'=max obj(i+3)=$$Save^ShmObj(.val)
 Set obj=9,obj(0)=$C(1,3)_$C(19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19,19)
 Set obj(1)=$C(8,1),obj(2)=$ZLC(0)
 Quit $$Save^ShmObj(.obj)
%CBYT2BIT Set bits=$ZBITSTR(256),from=""
 For  Set from=$o(^(from)) Quit:from=""  Set bits=$ZBITSET(bits,from+1,1,^(from))
 Set obj=7,obj(0)=$C(1,3,1)
 Set obj(1)=$C(9,1),obj(2)=$ZLC(0),obj(3)=$e(bits,2,33)
 Quit $$Save^ShmObj(.obj)
%CompVAL(ref,cvt) [ttyp,fromenc,name] PUBLIC {
 Set $ZT="ERROR"
 Set pref=$name(@ref@("bmp")),hi=""
 Set $p(obj(0),$c(0),257)="",obj=19
 For {
   Set hi=$o(@pref@(hi)) Quit:hi=""
   Set oid=$$%CompPAG($name(@pref@(hi)))
   If oid { Set obj(hi+1)=oid,$e(obj(0),hi+1)=$c(^SYS("shm","Obj",oid)) }
 }
 If $o(obj(1))="" {
   Set obj(0)=$C(1,3)_$E(obj(0)),obj(3)=obj(1)
   Set obj(1)=$C(11,cvt)
 } Else {
   Set oid=$$Save^ShmObj(.obj) Kill obj
   Set obj(0)=$C(1,3,19),obj(3)=oid
   Set obj(1)=$C(10,cvt)
   If $d(@ref@("sur")) {
     Set pref=$name(@ref@("sur")),hi=""
     Set $p(sur(0),$c(0),257)="",sur=29
     For {
	   Set hi=$o(@pref@(hi)) Quit:hi=""
	   Set oid=$$%CompPAG($name(@pref@(hi)),1024)
	   If oid { Set sur(hi+1)=oid,$e(sur(0),hi+1)=$c(^SYS("shm","Obj",oid)) }
     }
     Set oid=$$Save^ShmObj(.sur),obj(0)=obj(0)_$C(29),obj(4)=oid
   }
 }
 Set obj=9,obj(2)=$ZLC(0)
 Quit $$Save^ShmObj(.obj)
ERROR Set $ZT=""
 Do ErrMsg^NLSLOAD("Error while trying to create "_$s($g(ttyp)="XLT":"Translation",$g(ttyp)="XY":"$X/$Y Behaviour",$g(ttyp)="Ident":"Identifier",$g(ttyp)="Pattern":"Pattern Match",$g(ttyp)="UpperCase":"Upper case",$g(ttyp)="LowerCase":"Lower case",$g(ttyp)="TitleCase":"Title case",1:"Collation")_" table "_$g(fromenc)_" to "_$g(name)_".")
 Quit 0
}
%CompPAG(ref,siz) New max,obj,i,lo,val
 Set max=-1,lo=$o(@ref@("")),$ZT="%CompERROR",siz=$G(siz,256)-1
 For  Quit:lo=""  Set val=^(lo),lo=$o(^(lo)) Set:val>max max=val
 zt:(max<0)!(max>$ZH("ffffffff")) "COMP" Goto %CompPAG1:max<256,%CompPAG2:max<65536
 Set val="" For i=0:1:siz Set val=val_$ZLC($g(^(i)))
 Set obj=$S(siz=255:12,1:22),obj(0)=val Quit $$Save^ShmObj(.obj)
%CompPAG2 Set val="" For i=0:1:siz Set val=val_$ZWC($g(^(i)))
 Set obj=$S(siz=255:11,1:21),obj(0)=val Quit $$Save^ShmObj(.obj)
%CompPAG1 Quit:'max 0 Set val="" For i=0:1:siz Set val=val_$C($g(^(i)))
 Set obj=$S(siz=255:10,1:20),obj(0)=val Quit $$Save^ShmObj(.obj)
%CompPAGMulti(ref,flg,siz) New min,max,minlen,maxlen,obj,tmp,i,lo,val,typ,trz
 Set flg=+$g(flg),siz=$g(siz,256),$p(tmp(0),$c(0),257)="",trz=0
 Set minlen=65536,maxlen=-1,min=$ZH("100000000"),max=-1
 Set lo=$o(@ref@(""))
 For  Quit:lo=""  zt:lo'=+lo "COMP" Set lo=+lo Do  Set lo=$o(@ref@(lo))
 . Set val=@ref@(lo)
 . Set tmp(lo+1)=$$%CompStr(val,.typ,.min,.max,.minlen,.maxlen,.trz)
 . If 'lo,val=0,flg Set trz=0 ; Don't count 0->0 translation
 . Set $e(tmp(0),lo+1)=$C(typ)
 If max>255 Goto:minlen>0 %GenPAG2:maxlen=1,%GenPAKWRD4:maxlen=2
 Else  Goto:minlen>0 %GenPAG1:maxlen=1,%GenOFF:trz,%GenPAKBYT2:maxlen=2,%GenPAKBYT4:maxlen'>4
%GenOFF For i=1:1:siz Set obj=$A(tmp(0),i) Set:$d(tmp(i)) obj(0)=tmp(i),tmp(i)=$$Save^ShmObj(.obj)
 Set tmp=$S(siz=256:19,1:29) Quit $$Save^ShmObj(.tmp)
%GenPAG1 Set val="" For i=1:1:siz Set val=val_$E($g(tmp(i))_$c(0))
 Set obj=$S(siz=256:10,1:20) Set obj(0)=val Quit $$Save^ShmObj(.obj)
%GenPAG2 Set val="" For i=1:1:siz Set val=val_$E($g(tmp(i))_$zwc(0),1,2)
 Set obj=$S(siz=256:11,1:21) Set obj(0)=val Quit $$Save^ShmObj(.obj)
%GenPAKBYT2 Set val="" For i=1:1:siz Set val=val_$E($g(tmp(i))_$zwc(0),1,2)
 Set obj=$S(siz=256:14,1:24) Set obj(0)=val Quit $$Save^ShmObj(.obj)
%GenPAKBYT4 Set val="" For i=1:1:siz Set val=val_$E($g(tmp(i))_$zlc(0),1,4)
 Set obj=$S(siz=256:15,1:25) Set obj(0)=val Quit $$Save^ShmObj(.obj)
%GenPAKWRD4 Set val="" For i=1:1:siz Do
 . If $a(tmp(0),i)=5 Set tmp(i)=$zwc($a(tmp(i),1))_$zwc($a(tmp(i),2))
 . Set val=val_$E($g(tmp(i))_$zlc(0),1,4)
 Set obj=$S(siz=256:17,1:27) Set obj(0)=val Quit $$Save^ShmObj(.obj)
%CompStr(val,typ,pmin,pmax,minlen,maxlen,trz) New i,len,chr,str,min,max
 Set len=$s($l(val):$l(val,","),1:0),str="",min=65536,max=-1
 Set pmin=+$g(pmin),pmax=+$g(pmax),minlen=+$g(minlen),maxlen=+$g(maxlen),trz=+$g(trz)
 Set:len<minlen minlen=len Set:len>maxlen maxlen=len Set typ=5 Quit:'len ""
 For i=1:1:len Set chr=$p(val,",",i) zt:chr'=+chr "COMP" Set:chr<min min=+chr Set:chr>max max=+chr
 If max>255 Set typ=6 For i=1:1:len Set str=str_$zwc($p(val,",",i))
 Else  Set typ=5 For i=1:1:len Set str=str_$c($p(val,",",i))
 Set:+$p(val,",",len)=0 trz=1
 Set:min<pmin pmin=min Set:max>pmax pmax=max Quit str