Include %PMML

Class %DeepSee.PMML.ModelOutput Extends %RegisteredObject [ Abstract, System = 4 ]
{

Property %Trace [ Internal, MultiDimensional ];

Property %AllClassProbs [ Internal, MultiDimensional ];

/// 		pArray(fieldName) = $lb(feature, targetFieldName, displayName)
ClassMethod %GetFields(Output pFields) As %Status [ Abstract ]
{
	// implementation will be generated by %DeepSee.PMML.Definition.%CreateClasses()
}

/// Returns the feature value of predicted field <var>pTargetField</var>. If <var>pTargetField</var>
/// is left blank, the feature value for the "last" field is returned (generally safe for models
/// predicting a single field value). Note that <var>pTargetField</var> is the name of the field
/// in the data class, not in this output class.
/// <var>pAllValues</var> contains the feature values for all fields, indexed by field name
Method %GetFeatureValue(pFeature As %String = "predictedValue", pTargetField As %String = "", Output pAllValues) As %String
{
	set tFeatureValue = ""
	kill pAllValues
	do ..%GetFields(.tFields)
	set tField = "", pTargetField = $$$PROPERTYNAME(pTargetField)
	for {
		set tField = $order(tFields(tField),1,tDetails)
		quit:tField=""
		
		continue:($li(tDetails,1)'=pFeature)
		
		set tValue = $property($this, $$$PROPERTYNAME(tField))
		set pAllValues(tField) = tValue
		
		if (pTargetField=$$$PROPERTYNAME($li(tDetails,2))) || (pTargetField="") {
			set tFeatureValue = tValue
		}
	}
	quit tFeatureValue
}

/// Returns tracing information (if available) about the contribution of the input values to the 
/// model's outcome. 
/// Format:
/// 	pTrace(n) = $lb($lb(inputs), $lb(values), rank, [weight], [info])
Method %GetTracingInfo(Output pTrace) As %Status
{
	set tSC = $$$OK
	try {
		
		if $d(..%Trace)<10 {
			set tSC = $$$ERROR($$$GeneralError, "No trace info available")
			quit
		}
		
		merge pTrace = ..%Trace
		
	} catch (ex) {
		set tSC = ex.AsStatus()
	}
	quit tSC
}

Method %GetAllClassProbabilities(Output pClassProbs) As %Status [ Internal ]
{
	merge pClassProbs = ..%AllClassProbs
	quit $$$OK
}

}
